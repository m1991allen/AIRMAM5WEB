-- =============================================
-- 描述:	判斷MTS是否為MASTER
-- 記錄:	<2019/06/20><David.Sin><新增本預存>
-- =============================================

CREATE PROCEDURE [dbo].[spINSERT_L_WORKER_AS]

@FSWORK_NAME		varchar(64),
@FSWORKER			varchar(128)

AS

IF NOT EXISTS (SELECT * FROM [tbzWORKER] WHERE [FSWORK_NAME] = @FSWORK_NAME AND [FSWORKER] = @FSWORKER)
	BEGIN
		-- 如果沒有該筆資料的話，就先新增基本的資料，且預設非為 MASTER
		INSERT INTO [tbzWORKER] (FSWORK_NAME, FSWORKER, FDREGISTER_TIME, FNMASTER) VALUES (@FSWORK_NAME, @FSWORKER, GETDATE(), 0)
	END
ELSE
	BEGIN
		-- 僅更新註冊時間	
		UPDATE [tbzWORKER] SET FDREGISTER_TIME = GETDATE() WHERE [FSWORK_NAME] = @FSWORK_NAME AND [FSWORKER] = @FSWORKER
	END

-- 強制將逾時 3 分鐘的工作人員取消 Master 資格
UPDATE [tbzWORKER] SET [FNMASTER] = 0 WHERE [FNMASTER] = 1 AND [FDREGISTER_TIME] < DATEADD(MINUTE, -3, GETDATE())

-- 目前設定逾時為 3 分鐘，如果有需要的話可以視情況做延長的動作
IF NOT EXISTS (SELECT * FROM [tbzWORKER] WHERE [FSWORK_NAME] = @FSWORK_NAME AND [FNMASTER] = 1 AND [FDREGISTER_TIME] > DATEADD(MINUTE, -3, GETDATE()))
	BEGIN
		-- 將所有的人改為 SLAVE
		UPDATE [tbzWORKER] SET [FNMASTER] = 0 WHERE [FSWORK_NAME] = @FSWORK_NAME
		-- 將最新註冊的人選為 MASTER
		UPDATE [tbzWORKER] SET [FNMASTER] = 1 WHERE [FSWORK_NAME] = @FSWORK_NAME AND [FSWORKER] = @FSWORKER
	END

-- 回傳
SELECT * FROM [tbzWORKER] WHERE [FSWORK_NAME] = @FSWORK_NAME AND [FNMASTER] = 1

