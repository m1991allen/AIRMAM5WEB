-- =============================================
-- 描述:	取出調用申請中狀態為_T的資料
-- 記錄:	<2012/10/30><Dennis.Wen><新增本預存>
-- 記錄:	<2013/02/19><Eric.Huang><修改本預存將FILE_PATH改為正確的路徑>  
-- 記錄:	<2013/12/10><Eric.Huang><因應EBC需要加入流程:DAILY重轉時，採用TRANSCODE流程，故將TYPE為TRANSCODE加入到TMS流程中>
-- =============================================
CREATE PROCEDURE [dbo].[spGET_WORK_BOOKING_COPYFILE_STATUS_T]
AS
BEGIN
 	SET NOCOUNT ON;

		-- 2013/09/17 ++
	DECLARE @USER VARCHAR(20)
	DECLARE @FILE_SPACE_NAME VARCHAR(100)
	SET @USER = ISNULL((SELECT TOP 1 fsVALUE FROM tbzCONFIG WHERE (fsKEY = 'CUSTOMER_ID')),'')

	IF (@USER = 'EBC')
		BEGIN
			SET @FILE_SPACE_NAME = '/HVIDEO'
		END
	ELSE IF (@USER = 'EP')
		BEGIN
			SET @FILE_SPACE_NAME = '/1_HVIDEO'
		END
	ELSE IF (@USER = 'FTV')
		BEGIN
			SET @FILE_SPACE_NAME = '/HVideo'
		END

	ELSE
		BEGIN
			SET @FILE_SPACE_NAME = '/HVIDEO'
		END

	BEGIN TRY
		SELECT
			fnWORK_ID, fnGROUP_ID, fsTYPE, fsPARAMETERS, WK.fsSTATUS,
			fsPROGRESS, fsPRIORITY, fdSTIME, fdETIME, fsRESULT,
			fsNOTE, WK.fdCREATED_DATE, WK.fsCREATED_BY, WK.fdUPDATED_DATE, WK.fsUPDATED_BY,
			_ITEM_TYPE, _ITEM_ID, _sDESCRIPTION,
			
			_SM_FILE_PATH = @FILE_SPACE_NAME + (dbo.fnGET_TSM_PATH_BY_FILE_NO((select dbo.fnGET_WORK_PARAMETERS_ANALYZE_BOOKING(fsPARAMETERS,1,'N','N')))),
			_SM_FILE_SPACE_NAME = @FILE_SPACE_NAME,
			_SM_FILE_NAME  = (dbo.fnGET_TSM_PATH_BY_FILE_NO((select dbo.fnGET_WORK_PARAMETERS_ANALYZE_BOOKING(fsPARAMETERS,1,'N','N'))))
		FROM
			tblWORK AS WK
				LEFT JOIN tbmBOOKING AS BK ON (BK.fnBOOKING_ID = WK.fnGROUP_ID)
		WHERE
			-- 2013/12/10 ERIC ++
			--(WK.fsSTATUS = '_T') AND ((WK.fsTYPE IN ('BOOKING', 'COPYFILE', 'NAS', 'AVID') AND (BK.fsSTATUS LIKE '0%')) OR (WK.fsTYPE = 'TRANSCODE'))
			-- 2013/12/10 ERIC --
			WK.fdSTART_WORK_TIME <= GETDATE() AND (WK.fsSTATUS = '_T') AND (WK.fsTYPE IN ('BOOKING', 'COPYFILE', 'NAS', 'AVID')) AND (BK.fsSTATUS LIKE '0%')

		ORDER BY
			BK.fnORDER, WK.fsPRIORITY, BK.fnBOOKING_ID, WK.fnWORK_ID
	END TRY
	BEGIN CATCH
		SELECT RESULT = 'ERROR:' + CAST(@@ERROR AS VARCHAR(10)) + '-' + ERROR_MESSAGE()
	END CATCH
END